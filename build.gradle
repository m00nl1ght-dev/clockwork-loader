
plugins {
    id 'maven-publish'
    id 'extra-java-module-info'
}

allprojects {

    version '0.2'
    group 'dev.m00nl1ght.clockwork'

    ext {
        nightconfigVersion = "3.6.3"
        semverVersion = "3.1.0"
        log4jVersion = "2.13.3"
        fastutilVersion = "8.3.1"
    }

}

evaluationDependsOnChildren()

subprojects {

    apply plugin: "extra-java-module-info"

    repositories {
        mavenCentral()
    }

    extraJavaModuleInfo {
        automaticModule("toml-${nightconfigVersion}.jar", "nightconfig.toml")
        automaticModule("core-${nightconfigVersion}.jar", "nightconfig.core")
        automaticModule("semver4j-${semverVersion}.jar", "semver4j")
    }

}

publishing {

    repositories {
        maven {
            if (project.hasProperty('devMavenPassword')) {
                url 'https://m00nl1ght.dev/maven/'
                credentials {
                    username devMavenUser
                    password devMavenPassword
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }

    publications {
        core(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'core'
            version = rootProject.version
            from project('core').components.java
        }

        testEnv(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'test-env'
            version = rootProject.version
            from project('test-env').components.java
        }

        testPlugin(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'test-plugin'
            version = rootProject.version
            from project('test-plugin').components.java
        }
    }
}
