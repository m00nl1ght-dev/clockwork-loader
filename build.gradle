
allprojects {

    version '0.7'
    group 'dev.m00nl1ght.clockwork'

    ext {
        targetJDK = "14"
        nightconfigVersion = "3.6.2-j9m"
        mixinVersion = "0.8.4"
        log4jVersion = "2.13.3"
        slf4jVersion = "1.7.30"
    }

}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetJDK
    targetCompatibility = targetJDK

    java {
        withSourcesJar()
        // withJavadocJar()
    }

    sourceSets {
        main {
            output.resourcesDir = file('build/bin')
            java.outputDir = file('build/bin')
        }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:20.1.0'
    }

    test {
        useJUnitPlatform()
    }

    jar {
        manifest {
            from(new File(sourceSets.main.resources.getSrcDirs()[0], 'META-INF/MANIFEST.MF'))
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://m00nl1ght.dev/maven/" }
    }

    publishing {
        repositories {
            maven {
                if (project.hasProperty('devMavenPassword')) {
                    url 'https://m00nl1ght.dev/maven/'
                    credentials {
                        username devMavenUser
                        password devMavenPassword
                    }
                }
            }
        }
    }

}
