
plugins {
    id 'maven-publish'
    id 'extra-java-module-info'
}

evaluationDependsOnChildren()
version '0.2'

subprojects {

    apply plugin: "extra-java-module-info"

    version rootProject.version
    group 'dev.m00nl1ght.clockwork'

    repositories {
        mavenCentral()
    }

    extraJavaModuleInfo {
        automaticModule("toml-3.6.0.jar", "nightconfig.toml")
        automaticModule("core-3.6.0.jar", "nightconfig.core")
        automaticModule("semver4j-3.1.0.jar", "semver4j")
    }

}

publishing {

    repositories {
        maven {
            if (project.hasProperty('devMavenPassword')) {
                url 'https://m00nl1ght.dev/maven/'
                credentials {
                    username devMavenUser
                    password devMavenPassword
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }

    publications {
        core(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'core'
            version = rootProject.version
            from project('core').components.java
        }

        testEnv(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'test-env'
            version = rootProject.version
            from project('test-env').components.java
        }

        testPlugin(MavenPublication) {
            groupId = 'dev.m00nl1ght.clockwork'
            artifactId = 'test-plugin'
            version = rootProject.version
            from project('test-plugin').components.java
        }
    }
}
