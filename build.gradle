
allprojects {

    version '0.6'
    group 'dev.m00nl1ght.clockwork'

    ext {
        targetJDK = "14"
        nightconfigVersion = "3.6.2-j9m"
        log4jVersion = "2.13.3"
    }

}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetJDK
    targetCompatibility = targetJDK

    java {
        withSourcesJar()
        // withJavadocJar()
        modularity.getInferModulePath() set true
    }

    sourceSets {
        main {
            output.resourcesDir = file('build/bin')
            java.outputDir = file('build/bin')
        }
    }

    test {
        useJUnitPlatform()
    }

    jar {
        manifest {
            from(new File(sourceSets.main.resources.getSrcDirs()[0], 'META-INF/MANIFEST.MF'))
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://m00nl1ght.dev/maven/" }
    }

    publishing {
        repositories {
            maven {
                if (project.hasProperty('devMavenPassword')) {
                    url 'https://m00nl1ght.dev/maven/'
                    credentials {
                        username devMavenUser
                        password devMavenPassword
                    }
                }
            }
        }
    }

}
